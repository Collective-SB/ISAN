m="current pos" nf="Not found" fo="Waypoint Loaded" goto 1+(:WPCMD!="")                                         //wait for waypoint command
a=:WPCMD :WPCMD="parsing" s=9843 b=1 i=0 x=0 goto 3+3*((a-" ")==a)          //store current, do parser setup, if has space, waypoint -> parser
c=a c-=a-- d=5*(c>4)+2*(s>(s-c)) d+=c>d x+=(d+c>d)*b b*=10 goto 4+c<0       //parser
x*=1-2*(c>" ") a-=" "-c b=1 z=y/(i++<3) y=x x=0 goto 3                      //parser
:DL=l :DX=x :DY=y :DZ=z :WPCMD="parsed" :WPcw=10 goto 20                    //parser
r=8+3*(a=="cur")+4*(a=="zero")                                              //command or waypoint name?
goto 7+(:HSi==0)*(r-7)                                                      //Wait for HS, then goto r                                                                             
:HSl=a :HSc=1 r=17 goto 7                                                   //CMD: load waypoint
:DL=a-"save_" :HSl=:DL :HSc=2 r=18 goto 7                                   //CMD: save_
:HSl=a-"del_" :HSc=3 r=19 goto 7                                            //CMD: del_
w=1000 :DL=m :DX=:X/w*w :DY=:Y/w*w :DZ=:Z/w*w :WPCMD=m :WPcw=10 goto 20     //CMD: cur
:DL="zero" :DX=0 :DY=0 :DZ=0 :WPCMD="zero" :WPcw=10 goto 20                 //CMD: zero




a=:HSo if a=="" then :WPCMD=nf b=20 :WPcw=10 else :WPCMD=fo b=3 end goto b
:WPCMD="Saved" :WPcw=10 goto 20
:WPCMD="Deleted" :WPcw=10 :DL=tmp
:WPCMD="" goto 1

//waypoint_name         : load "waypoint_name"
//waypoint_name X Y Z   : just parse    -> (:DL :DX :DY :DZ)
//save_waypoint_name    : save currently loaded as "waypoint_name"
//del_waypoint_name     : delete "waypoint_name"
//cur                   : load current position ("cur", :X :Y :Z) -> (:DL :DX :DY :DZ)
//zero                  : load ("", 0,0,0) -> (:DL :DX :DY :DZ)


//:WPCMD="save_test"
    // 2:->6 (no space)
    // 6:r=9
    // 7: wait for HSc==0, ->r(9)
    // 9: :DL="test" :HSl="-not saved-" :HSc=2 r=18 ->7
    // 7: wait for HSc==0, ->r(18)
    //18: :WPCMD="Saved" :WPcw=10 ->20
    // -- chipwait 10 --
    //20: :WPCMD="" ->1