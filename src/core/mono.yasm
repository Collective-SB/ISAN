#import core/includes/magics.yasm
#import core/includes/transmitterGlobals.yasm

## map above globals with nice functions (T_target & T_magic)
#import core/includes/mappingFunctions.yasm

## get distance from given transmitter
## rem: set signalOffset in setup!
#define getDistance(id) {
    (signalOffset-GLOBAL_TRANSMITTER_{id}_SIGNAL)
}

#define measurementHandler(nextMessage, axis, nextJump) {
    T_target(A)={nextMessage};
    {axis}=getDistance(A)^2;
    {axis}_rate = ({axis} - last_{axis})/4;
    {axis}_rate_rate = ({axis}_rate - last_{axis}_rate)/4;
    last_{axis} = {axis};
    last_{axis}_rate = {axis}_rate;
    jump = {nextJump};
}

#define predictForward(axis){
    {axis}_rate+={axis}_rate_rate;
    {axis}+={axis}_rate;
}

##--------------------------- CODE START ---------------------------##
@setup_transmitters_1:
    messageD = MAGICS_TRANSMITTER_PREFIX;
    messageA = messageD + T_magic(A);
    messageB = messageD + T_magic(B);

@setup_transmitters_2:
    messageC = messageD + T_magic(C);
    messageD += T_magic(D);
    signalOffset=999999;
    T_target(A) = messageA

@setup_magics_x:
    MAGICS(X, e, f, g, h, i);

@setup_magics_y:
    MAGICS(Y, j, k, l, m, n);

@setup_magics_z:
    MAGICS(Z, o, p, q, r, s);

@measure_A:
    measurementHandler(messageB, a, measure_B);
    goto predict_forward;

@measure_B:
    measurementHandler(messageC, b, measure_C);
    goto predict_forward;

@measure_C:
    measurementHandler(messageD, c, measure_D);
    goto predict_forward;

@measure_D:
    measurementHandler(messageA, d, measure_A);
    goto predict_forward;

@predict_forward:
    predictForward(a);
    predictForward(b);
    predictForward(c);
    predictForward(d);

@calc_position:
    :x = a/e + b/f + c/g + d/h - i;
    :y = a/j + b/k + c/l + d/m - n;
    :z = a/o + b/p + c/q + d/r - s;
    goto jump;
