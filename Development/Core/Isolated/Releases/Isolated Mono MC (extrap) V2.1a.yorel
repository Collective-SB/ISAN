//Name:             Isolated Mono MC, with extrapolation
//Current Version:      V2.1a
//Credit:               Azur

//Globals
    N, M, R     - Receiver fields
    A, B, C, D  - Distance Measurement Cache
    E, F, G, H  - Cache Times
    u, v, w, x  - extrapolated distance (& partial axis calc)

    X, Y, Z     - Core position

----+----|----+----|----+----|----+----|----+----|----+----|----+----|
z="station_" u=z+"hq_imperial_a" v=z+"hq_kingdom_a" :M=u
w=z+"proving_grounds" x=z+"capital_imperial_a" y=999999
if :N==u then :A=y-:R :E=:T++ :M=v goto 4 end :D=y-:R :H=:T++ goto 3
if :N==v then :B=y-:R :F=:T++ :M=w goto 5 end :A=y-:R :E=:T++ goto 4
if :N==w then :C=y-:R :G=:T++ :M=x goto 6 end :B=y-:R :F=:T++ goto 5
if :N==x then :D=y-:R :H=:T++ :M=u goto 3 end :C=y-:R :G=:T++ goto 6
//ISANv2_IsoMonoMC_scanner_2.3a                         THE COLLECTIVE
----+----|----+----|----+----|----+----|----+----|----+----|----+----|
a=:A^2 b=0 q=122828033.859
if :E!=b then c=a d=b end a=:A^2 b=:E :u=a+(a-c)*(:T-b)/(b-d)-q goto 2
//ISANv2_IsoMonoMC_extrapA_2.4a                         THE COLLECTIVE
----+----|----+----|----+----|----+----|----+----|----+----|----+----|
a=:B^2 b=0 r=121953134.66
if :F!=b then c=a d=b end a=:B^2 b=:F :v=a+(a-c)*(:T-b)/(b-d)-r goto 2
//ISANv2_IsoMonoMC_extrapB_2.4a                         THE COLLECTIVE
----+----|----+----|----+----|----+----|----+----|----+----|----+----|
a=:C^2 b=0 s=2443344462.251
if :G!=b then c=a d=b end a=:C^2 b=:G :w=a+(a-c)*(:T-b)/(b-d)-s goto 2
//ISANv2_IsoMonoMC_extrapC_2.4a                         THE COLLECTIVE
----+----|----+----|----+----|----+----|----+----|----+----|----+----|
a=:D^2 b=0  t=858880.223
if :H!=b then c=a d=b end a=:D^2 b=:H :x=a+(a-c)*(:T-b)/(b-d)-t goto 2
//ISANv2_IsoMonoMC_extrapD_2.4a                         THE COLLECTIVE
----+----|----+----|----+----|----+----|----+----|----+----|----+----|
i=214589.634 a=1.836 b=2.792 n=-4787.429 o=-16480.662 p=1602.298
c=10.285 e=39665.376 f=-39665.376 j=-68626.15 k=100891.402 m=-2820.508
:X=:u/e+:v/f :Y=:u/i+:v/j+:w/k :Z=:u/m+:v/n+:w/o+:x/p goto 3
//ISAN 2.0:IsoMonoAxP_2.1a                              THE COLLECTIVE
----+----|----+----|----+----|----+----|----+----|----+----|----+----|